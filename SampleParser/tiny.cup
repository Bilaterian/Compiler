/*
  Created by: Fei Song
  File Name: tiny.cup
  To Build: java java_cup.Main < tiny.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
//import java_cup.runtime.*;
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal IF, ELSE, INT, RETURN, VOID, WHILE;
terminal PLUS, MINUS, MULTIPLY, DIVIDE, LESSTHAN, LESSTHANEQUAL, GREATERTHAN, GREATERTHANEQUAL;
terminal EQUALEQUAL, NOTEQUAL, EQUAL, SEMI, COMMA, LPAREN, RPAREN, SQLPAREN, SQRPAREN, CRLPAREN, CRRPAREN;
terminal String ID;
terminal String NUM;
terminal ERROR;

non terminal ExpList statementlist, arglist, args;
non terminal Exp expressionstmt, selectionstmt, iterationstmt, returnstmt;
non terminal Exp simpleexpression, additiveexpression;
non terminal Exp statement, expression, relop,  term,  factor, call;
non terminal NameTy typespecifier;
non terminal Dec declaration, fundeclaration;
non terminal DecList declarationlist, program;
non terminal Var var;
non terminal VarDecList params, localdeclarations, paramlist;
non terminal VarDec param, vardeclaration;
non terminal CompoundExp compoundstmt;
non terminal Integer addop,mulop;

precedence nonassoc NOTEQUAL, LESSTHANEQUAL, LESSTHAN, GREATERTHANEQUAL, GREATERTHAN, EQUAL;
precedence left PLUS, MINUS;
precedence left MULTIPLY, DIVIDE;
precedence left ELSE;

/* ----------------------------Grammar Section-------------------- */


start with program;

program				::= declarationlist:dl 
						{: RESULT = dl; :}
					| 	ERROR
                     {:parser.report_fatal_error("invalid file format", null); :}
					
					; 


declarationlist		::= declarationlist:dl declaration:d
						{: DecList t = dl;
							while( t.tail != null )
								t = t.tail;
							t.tail = new DecList(d, null);
							RESULT = dl;
						:}
					  | declaration:d 
						{: RESULT = new DecList(d, null); :}
					  | error:e SEMI declaration:d 
					  	{:parser.report_error ("Invalid Syntax - declarationlist", e);
						  RESULT = new DecList(d,null); :}
					  ;

declaration			::= vardeclaration:v
						{:RESULT = v; :}
					  | fundeclaration:f 
						{:RESULT = f; :}
					  ;

vardeclaration		::= typespecifier:t ID:i SEMI
						{: RESULT = new SimpleDec(tleft, tright, t, i); :}
					  | typespecifier:t ID:i SQLPAREN NUM:n SQRPAREN SEMI
						{: 	IntExp num = new IntExp(nleft, nright, n);
							RESULT = new ArrayDec(tleft, tright, t, i, num); 
						:}
					  | typespecifier:t ID:i SQLPAREN error:e SQRPAREN SEMI
						{: 	 parser.report_error("VarDeclartion: invalid array size at row:" + (eleft + 1) + ",col:" + eright + "", e);
							//RESULT = new ArrayDec(tleft, tright, t, i, null); 
						:}
					  | typespecifier:t ID:i error:e
					  
						{: 	 parser.report_error("VarDeclartion: missing semi colon at {row:" + (eleft + 1) + ",col:" + eright + "}", e); 
							
						:}

					  ;

typespecifier		::= INT:i  
						{: RESULT = new NameTy(ileft, iright, 0); :}
					  | VOID:v 
						{: RESULT = new NameTy(vleft, vright, 1); :}
					  ;

fundeclaration		::= typespecifier:t ID:i LPAREN params:p RPAREN compoundstmt:c 
						{: RESULT = new FunctionDec(tleft, tright, t, i, p, c); :}
					   | typespecifier:t ID:i LPAREN error:e RPAREN compoundstmt:c 
						{: parser.report_error("FunDeclaration: Invalid Params at {row:" + (eleft + 1) + ",col:" + eright + "}", e); 
						RESULT = new FunctionDec(tleft, tright, t, i, null, null); :}
						


					  ;

params				::= paramlist:pl
						{: RESULT = pl; :}
					  | VOID
						{: RESULT = null; :}
					  ;

paramlist			::= paramlist:pl COMMA param:p 
						{: VarDecList t = pl;
							while( t.tail != null )
								t = t.tail;
							t.tail = new VarDecList(p, null);
							RESULT = pl;
						:}
					  | param:p
						{: RESULT = new VarDecList(p, null); :}
					  ;

param				::= typespecifier:t ID:i 
						{: RESULT = new SimpleDec(tleft, tright, t, i); :}
					  | typespecifier:t ID:i SQLPAREN SQRPAREN 
						{:  IntExp ie = new IntExp(tleft, tright, "");
							RESULT = new ArrayDec(tleft, tright, t, i, ie); :}
					  | typespecifier:t ID:i SQLPAREN NUM:n SQRPAREN 
						{:  IntExp ie = new IntExp(tleft, tright, n);
							RESULT = new ArrayDec(tleft, tright, t, i, ie); :}
					  ;

compoundstmt		::= CRLPAREN:c localdeclarations:l statementlist:sl CRRPAREN 
						{:RESULT = new CompoundExp(cleft, cright, l, sl); :}
						| CRLPAREN:c localdeclarations:l  CRRPAREN 
						{:RESULT = new CompoundExp(cleft, cright, l, null);:}	
						| CRLPAREN:c statementlist:sl CRRPAREN 
						{:RESULT = new CompoundExp(cleft, cright, null, sl);:}
						| CRLPAREN:c CRRPAREN 
						{:RESULT = new CompoundExp(cleft,cright, null, null);:}

					  ;

localdeclarations	::= localdeclarations:l vardeclaration:v
							{:
							VarDecList t = l;
							while( t.tail != null)
								t = t.tail;
							t.tail = new VarDecList(v,null);
							RESULT = l;:}
					  |
						vardeclaration:v
						{:
							RESULT = new VarDecList(v, null); :}
					
					  ;

statementlist		::= statementlist:sl statement:s 
						{:
							ExpList t = sl;
							while( t.tail != null)
								t = t.tail;
							t.tail = new ExpList(s, null);
							RESULT = sl;
						:}
					  |
						statement:s
						{:
							RESULT = new ExpList(s, null); :}
					  ;

statement			::= expressionstmt:e
						{: RESULT = e; :}
					  | compoundstmt:c
						{: RESULT = c; :}
					  | selectionstmt:s
						{: RESULT = s; :}
					  | iterationstmt:i
						{: RESULT = i; :}
					  | returnstmt:r
						{: RESULT = r; :}
					  | error:e SEMI
					  {:
					  	parser.report_error("Invalid Statement at {row:" + (eleft + 1) + ",col:" + eright + "}", e); 
					  :}
					  ;

expressionstmt		::= expression:e SEMI 
						{: RESULT = e; :}
					  | SEMI 
						
					  ;

selectionstmt      ::= IF:i LPAREN expression:e RPAREN statement:s
						{: RESULT = new IfExp(ileft, iright, e, s, null); :}
					 | IF:i LPAREN expression:e RPAREN statement:s1 ELSE statement:s2
						{: RESULT = new IfExp(ileft, iright, e, s1, s2); :}
					 ;
					 

iterationstmt		::= WHILE:w LPAREN expression:e RPAREN statement:s
						{: RESULT = new WhileExp(wleft,wright, e, s); :}
						| WHILE:w error:e expression:exp RPAREN statement:s 
						{: parser.report_error("Missing LPAREN at {row:" + (eleft + 1) + ",col:" + eright + "}", e); :}
						| WHILE:w LPAREN error:e RPAREN statement:s 
						{: parser.report_error("Invalid Expression at {row:" + (eleft + 1) + ",col:" + eright + "}", e); :}
						
					  ;

returnstmt			::= RETURN:r expression:e SEMI 
						{: RESULT = new ReturnExp(rleft, rright, e); :}
					  | RETURN:r SEMI 
						{: RESULT = new ReturnExp(rleft, rright, null); :}
					  ;

expression			::= var:v EQUAL expression:e
						{: RESULT = new AssignExp(vleft, vright, new VarExp(vleft,vright,v),e); :}
					  | simpleexpression:s 
						{:RESULT = s; :}
					  ;

var					::= ID:i
						{: RESULT = new SimpleVar(ileft, iright, i); :}
					  | ID:i SQLPAREN expression:e SQRPAREN 
						{: RESULT = new IndexVar(ileft, iright, i, e); :}
					  ;

simpleexpression	::= additiveexpression:a1 LESSTHANEQUAL additiveexpression:a2
						{: RESULT = new OpExp(a1left, a1right, a1, OpExp.LE, a2); :}
					  | additiveexpression:a1 LESSTHAN additiveexpression:a2
						{: RESULT = new OpExp(a1left, a1right, a1, OpExp.LT, a2); :}
					  | additiveexpression:a1 GREATERTHAN additiveexpression:a2
						{: RESULT = new OpExp(a1left, a1right, a1, OpExp.GT, a2); :}
					  | additiveexpression:a1 GREATERTHANEQUAL additiveexpression:a2
						{: RESULT = new OpExp(a1left, a1right, a1, OpExp.GE, a2); :}
					  | additiveexpression:a1 EQUALEQUAL additiveexpression:a2
						{: RESULT = new OpExp(a1left, a1right, a1, OpExp.EQ, a2); :}
					  | additiveexpression:a1 NOTEQUAL additiveexpression:a2
						{: RESULT = new OpExp(a1left, a1right, a1, OpExp.NE, a2); :}
					  | additiveexpression:a 
						{:RESULT = a; :}
					  ;

relop				::= LESSTHANEQUAL
					  | LESSTHAN 
					  | GREATERTHAN 
					  | GREATERTHANEQUAL 
					  | EQUALEQUAL 
					  | NOTEQUAL
					  ;

additiveexpression	::= additiveexpression:ae addop:o term:t
						{: RESULT = new OpExp(aeleft, aeright, ae, o, t); :}
					  | term:t 
						{: RESULT = t; :}
					  ;

addop				::= PLUS: i 
						{: RESULT = OpExp.PLUS; :}
					  | MINUS: i
						{: RESULT = OpExp.MINUS;:}
					;


term				::= term:t mulop:o factor:f 
						{: RESULT = new OpExp(tleft, tright, t, o, f); :}
					  | factor:f 
						{: RESULT = f; :}
					  ;

mulop				::= MULTIPLY: i 
						{: RESULT = OpExp.MUL; :}
					  | DIVIDE: i
						{: RESULT = OpExp.DIV;:}
					;

factor				::= LPAREN expression:e RPAREN
						{: RESULT = e; :}
					  | var:v 
						{: RESULT = new VarExp(vleft, vright, v); :}
					  | call:c 
						{: RESULT = c; :}
					  | NUM:n
						{: RESULT = new IntExp(nleft, nright, n); :}
					  ;

call				::= ID:i LPAREN args:a RPAREN 
						{:RESULT = new CallExp(ileft, iright, i, a); :}
					  ;

args				::= arglist:al
						{:RESULT = al; :}
					  | 
						
					  ;

arglist				::= arglist:al COMMA expression:e 
						{:RESULT = new ExpList(e, al); :}
					  | expression:e 
						{:RESULT = new ExpList(e, null); :}
					  ;
